#! /bin/csh -f
#
# Install SPMS Software Project Management System
#

# DESTDIR is the one possible arg
set D =
if ($#argv > 0) set D = $1

if (!(-d $D/usr/new)) then
	echo "$D/usr/new: No such directory"
	exit(1)
endif
if (!(-d $D/usr/new/lib)) then
	echo "$D/usr/new/lib: No such directory"
	exit(1)
endif
if (!(-d $D/usr/man/mann)) then
	echo "$D/usr/man/mann: No such directory"
	exit(1)
endif

cd ..
setenv PROJECT $cwd

set PATH = (/usr/new /usr/ucb /bin /usr/bin)

#install libraries
foreach p (libhash libpdb libslist libspms libtree)
	cd $PROJECT/src/lib/$p/src
	sed -e '/###/,$d' < Makefile > $$Mf
	mv $$Mf Makefile
	make install
end

#install lib programs & data files in /usr/new/lib
foreach p (pgrep phelp)
	cd $PROJECT/src/lib/$p
	sed -e '/###/,$d' < Makefile > $$Mf
	mv $$Mf Makefile
	make DEST=${D}/usr/new/lib install
end

#install programs in /usr/new
cd $PROJECT/src/bin
foreach p (*)
	cd $PROJECT/src/bin/$p
	sed -e '/###/,$d' < Makefile > $$Mf
	mv $$Mf Makefile
	make DEST=${D}/usr/new install
end

#install manual pages in /usr/man/mann
cp $PROJECT/man/mann/*.n $D/usr/man/mann

exit(0)
