#	Install New Help Topics.
# Called by help as:  csh -f do.new dir [ topic ] ...

set subtree = $1:h			# get subtree by removing /cat tail
shift					# throw away directory argument
source $subtree/maint/config		# configure system
cd $subtree/src
if ($#argv == 0) then
cat << %
Usage:  help -m new topic ...
where topic is a file of nroff (-mayday) source in the directory
$subtree/src.  A topic called . will re-install all topics.
%
exit 1
else if ($1 == .) then
	echo -n "Re-installing ALL topics?  Type $$ to confirm: "
	if ($< != $$) then
		echo "Not confirmed.  Bye."
		exit 1
	endif
	set argv = (.)
endif
set tmp
foreach t ($*)
	if ($t == index_help)   set there_already
	set tmp = ($tmp `find $t -print`)
end
set argv = ($tmp)
echo "Correct the following if necessary.  Defaults are shown in ()."
if (! $?there_already) then
	echo -n "Do you want a new index_help? (n) "
	if ($< =~ y*)   set argv = ($argv index_help)
endif
echo -n "Topics? ($*) "
set tmp = ($<)
if ("$tmp" != "")   set argv = ($tmp)
echo -n "Sources to? ($srcmach) "
set tmp = ($<)
if ("$tmp" != "")   set srcmach = ($tmp)
echo -n "Catables to? ($catmach) "
set tmp = ($<)
if ("$tmp" != "")   set catmach = ($tmp)
echo -n "Old catables to? ($oldmach) "
set tmp = ($<)
if ("$tmp" != "")   set oldmach = ($tmp)
echo "OK.  Backgrounding."
source $subtree/maint/new &			# the real work
